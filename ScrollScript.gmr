?start: instruction+

?instruction: print_statement
        | declaration
        | assignment


# ----- Keywords -----

LET.2: "rune"
PRINT.2: "chant"
INPUT.2: "channel"
prev.2: FROM DAYS OF YORE
FROM.2: "from"
DAYS.2: "days"
OF.2: "of"
YORE.2: "yore"
TRUE.2: "True"
FALSE.2: "False"


# ----- Assignments & Declarations -----

declaration: LET var_name ["=" expression]

assignment: var_name "=" expression


# ----- Expressions -----

?expression: input_statement
        | term
        | "-" term                      -> un_expr_negate
        | expression ADD_OP term        -> bin_expr_add
        | expression OR term            -> bin_expr_or

?term: factor
        | term MUL_OP factor    -> bin_expr_mul
        | term AND factor       -> bin_expr_and

?factor: power
        | NOT factor                    -> un_expr_not
        | factor "^" power              -> bin_expr_pow
        | "(" expression ")"            -> group_expr

?power: BOOLEAN
        | STRING
        | var_expr
        | number

?number: INTEGER | FLOAT


# ----- Data Types -----

INTEGER.2: ["-"] DIGIT+

FLOAT.2: ["-"] DIGIT+ "." DIGIT+

STRING.2: "\"" /[^\"]*/ "\""

BOOLEAN.2: TRUE | FALSE

DIGIT.2: "0".."9"


# ----- Operators -----

ADD_OP: "+" | "-"

MUL_OP: "*" | "/" | "%"

AND: "&"

OR: "|"

NOT: "!"

# ----- Variables -----

var_name: VARIABLE

var_expr: VARIABLE prev         -> previous
        | VARIABLE

VARIABLE.1: /[a-zA-Z_][a-zA-Z0-9_]*/


# ----- Input & Output -----

print_statement: PRINT expression

input_statement: INPUT
                | INPUT expression    -> prompted_input


# ----- Features -----


# ----- Comments -----

COMMENT: /~~[^\n]*/
%ignore COMMENT

%import common.WS
%ignore WS